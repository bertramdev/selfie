buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "io.github.gradle-nexus:publish-plugin:1.0.0"
    }
}

plugins {
    id "io.spring.dependency-management" version "0.3.1.RELEASE"
    id "com.jfrog.bintray" version "1.6"
}


version "2.1.5"
group = 'com.bertramlabs.plugins'

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"org.grails.grails-plugin"
apply plugin:"maven-publish"
apply plugin:"java-library"
apply plugin:"signing"


ext {
    grailsVersion = project.grailsVersion
    isBuildSnapshot = version.endsWith('-SNAPSHOT')
    isReleaseVersion = !isBuildSnapshot
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    api "org.grails:grails-core"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"
    implementation "org.grails:grails-dependencies"
    implementation "org.grails:grails-web-boot"
    implementation "org.grails.plugins:cache"
    api 'org.imgscalr:imgscalr-lib:4.2'
    api 'com.bertramlabs.plugins:karman-core:1.5.6'
    api 'com.bertramlabs.plugins:karman-grails:1.5.6'

    console "org.grails:grails-console"

    profile "org.grails.profiles:web-plugin"
    implementation "org.grails:grails-plugin-services"
    api "org.grails:grails-plugin-domain-class"
    implementation "org.grails.plugins:hibernate5"
    implementation "org.hibernate:hibernate-core:5.4.0.Final"
    implementation "org.hibernate:hibernate-ehcache:5.4.0.Final"
    implementation "com.h2database:h2"

    testCompile "org.grails.plugins:hibernate5"
    testCompile "org.hibernate:hibernate-core:5.4.0.Final"
    testCompile "org.hibernate:hibernate-ehcache:5.4.0.Final"
    testCompile "com.h2database:h2"
//    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails:grails-datastore-gorm-test"
    testCompile "org.grails:grails-gorm-testing-support"
    testCompile "org.grails:grails-web-testing-support"
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    exclude 'com/bertramlabs/plugins/selfie/test/**'
}

if (isReleaseVersion) {
    apply plugin: "io.github.gradle-nexus.publish-plugin"
    nexusPublishing {
        repositories {
            sonatype {
                if(project.hasProperty('mavenUser')) {
                    username = mavenUser
                    password = mavenPassword
                }    
            }
        }
    }
} else {

}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'selfie'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'Selfie'
                    description 'Selfie is a Grails Image / File Upload Plugin. Use Selfie to attach files to your domain models, upload to a CDN, validate content, produce thumbnails.'
                    url 'https://github.com/bertramdev/selfie'
                    scm {
                        url 'https://github.com/bertramdev/selfie'
                        connection 'scm:https://bertramdev@github.com/bertramdev/selfie.git'
                        developerConnection 'scm:git://github.com/bertramdev/selfie.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'davydotcom'
                            name 'David Estes'
                            email 'davydotcom@gmail.com'
                        }
                    }
                }
            }
            from components.java
        }
    }
    repositories {
        maven {
            if( !isReleaseVersion ) {
                url = "http://nexus.bertramlabs.com/content/repositories/snapshots"
                if(project.hasProperty('labsNexusUser')) {
                    credentials {
                        username = labsNexusUser
                        password = labsNexusPassword
                    }
                }
            }
        }
    }
}



afterEvaluate {
    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
        sign publishing.publications.maven
    }
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

//do not generate extra load on Nexus with new staging repository if signing fails
tasks.withType(io.github.gradlenexus.publishplugin.InitializeNexusStagingRepository).configureEach {
    shouldRunAfter(tasks.withType(Sign))
}


test {
    systemProperties System.properties
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
        events 'started', 'passed'
    }
}


// grailsPublish {
//   if(project.hasProperty('bintrayUser')) {
//     user = bintrayUser
//     key = bintrayKey
//   }

//   userOrg = 'bertramlabs'
//     githubSlug = 'bertramdev/selfie'
//     license {
//         name = 'Apache-2.0'
//     }
//     title = "Selfie"
//     desc = "Selfie is a Grails Image / File Upload Plugin. Use Selfie to attach files to your domain models, upload to a CDN, validate content, produce thumbnails."
//     developers = [davydotcom:"David Estes"]
// }

